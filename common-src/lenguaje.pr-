function string lenguaje_sistema();
private
	fp;
	string id_lenguaje;
	string cadena_lenguaje;
	string cadena_lenguaje_bien;
	int primera_letra_lenguaje;
	string env_lang;
	string argumentos[10];
begin
	//PODEMOS ADIVINAR EL LENGUAJE! :D
	if(os_id==os_win32) //windows
		//qué lenguaje lleva este windows?
		if(!fexists(getenv("TEMP")+"\lang.txt")) 
			fp=fopen(getenv("TEMP")+"\language.bat",O_WRITE);
			fputs(fp,"reg query HKEY_LOCAL_MACHINE\SYSTEM\ControlSet001\Control\Nls\Language /v Default | findstr Default > %TEMP%\lang.txt");
			fclose(fp);
			exec(_P_WAIT,getenv("TEMP")+"\language.bat",0,0); 
		end
		fp=fopen(getenv("TEMP")+"\lang.txt",O_READ);
		if(fp) 
			cadena_lenguaje=fgets(fp);
			fclose(fp);
		end
		primera_letra_lenguaje=find(cadena_lenguaje,"0",0);
		cadena_lenguaje_bien=""+cadena_lenguaje[primera_letra_lenguaje+2]+cadena_lenguaje[primera_letra_lenguaje+3];

		switch(cadena_lenguaje_bien)
			case "09": id_lenguaje="en"; end
			case "0a": id_lenguaje="es"; end
			case "10": id_lenguaje="it"; end
			case "07": id_lenguaje="de"; end
			case "0c": id_lenguaje="fr"; end
			case "03": id_lenguaje="ca"; end
		end
	end
	if(os_id==os_linux) //linux o Android!
		// Aportado por Miry
		env_lang=getenv("LANG");
		env_lang=""+env_lang[0]+env_lang[1];
		id_lenguaje=env_lang;
	end
	if(os_id==1003)
		fp=fopen(savegamedir+"lang.txt",O_READ);
		if(fp) 
			cadena_lenguaje=fgets(fp);
			fclose(fp);
		end
		id_lenguaje=cadena_lenguaje;
		say("Idioma detectado: "+id_lenguaje);
	end
	if(os_id==os_caanoo)
		fp=fopen("/mnt/ubifs/usr/gp2x/common.ini",O_READ);
		if(fp)
			while(!feof(fp))
				cadena_lenguaje=fgets(fp);
				if(find(cadena_lenguaje,"language")>-1)
					env_lang=""+cadena_lenguaje[11]+cadena_lenguaje[12];
					break;
				end
			end
			fclose(fp);
		end
		id_lenguaje=env_lang;
	end
	return id_lenguaje;
end